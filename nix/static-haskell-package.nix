# Derive a fully static Haskell package based on musl instead of glibc.
{ nixpkgs, compiler, patches, allOverlays }:

periodic-common: periodic-client: periodic-server: periodic-client-exe:
let
  # The nh2/static-haskell-nix project does all the hard work for us.
  static-haskell-nix =
    let
      rev = "749707fc90b781c3e653e67917a7d571fe82ae7b";
    in
    builtins.fetchTarball {
      url = "https://github.com/nh2/static-haskell-nix/archive/${rev}.tar.gz";
      sha256 = "155spda2lww378bhx68w6dxwqd5y6s9kin3qbgl2m23r3vmk3m3w";
    };

    patched-static-haskell-nix =
      patches.applyPatches "patched-static-haskell-nix"
        static-haskell-nix
        [
          patches.periodic-openssl-linking-fix
        ];

  # Fix taken from https://github.com/PostgREST/postgrest/blob/43d71e95ac091aa77ac104de7fc881226d1a17f6/nix/static-haskell-package.nix
  # I'm not too sure if there are really needed for this simple project
  patchedNixpkgs =
    patches.applyPatches "patched-nixpkgs"
      nixpkgs
      [
        patches.nixpkgs-openssl-split-runtime-dependencies-of-static-builds
        patches.nixpkgs-gdb-fix-libintl
      ];

  pkgs = import nixpkgs { };
  lib = pkgs.haskell.lib;

  # We are defining our package by calling callCabal2nix on the `package.yml` generated by stack
  extraOverrides = final: prev:
    rec {
      "${periodic-common.name}" = prev.callCabal2nix "${periodic-common.name}" periodic-common.src {};
      "${periodic-client.name}" = prev.callCabal2nix "${periodic-client.name}" periodic-client.src {};
      "${periodic-server.name}" = prev.callCabal2nix "${periodic-server.name}" periodic-server.src {};
      "${periodic-client-exe.name}" = prev.callCabal2nix "${periodic-client-exe.name}" periodic-client-exe.src {};
    };

  # We make sure our package will be integrated in the nh2/patched-static-haskell-nix project
  overlays = [
    (allOverlays.haskell-packages { inherit compiler extraOverrides; })
  ];

  normalPkgs = import patchedNixpkgs { inherit overlays; };

  # each version of GHC needs a specific version of Cabal.
  defaultCabalPackageVersionComingWithGhc =
    {
      ghc884 = "Cabal_3_2_1_0";
    }."${compiler}";

  # The static-haskell-nix 'survey' derives a full static set of Haskell
  # packages, applying fixes where necessary.
  survey = import "${patched-static-haskell-nix}/survey" { inherit normalPkgs compiler defaultCabalPackageVersionComingWithGhc; };
in
{
  "${periodic-server.name}" = survey.haskellPackages."${periodic-server.name}";
  "${periodic-client-exe.name}" = survey.haskellPackages."${periodic-client-exe.name}";
}
